# If you run into this issue:
#
# All overlays passed to nixpkgs must be functions.
#
# 1. Move this file (ie default.nix.old).
# 2. Run the installer/switch again.
# 3. Observe it break on overlays/default.nix not existing.
# 4. Move the file back.
# 5. Run the installer/switch yet another time.
#
# Hopefully we one day find out what is causing this.
#
# See: https://github.com/NixOS/nix/issues/8443
{ flake-inputs, system, ... }:
[
  flake-inputs.dns-smart-block.overlays.default
  flake-inputs.nur.overlays.default
  (import ./test-script.nix)
  (import ./battlescribe-update-data.nix)
  (import ./blueutil.nix)
  (import ./cacert.nix)
  (import ./discord.nix)
  # (import ./crystal.nix)
  (import ./hiera-eyaml.nix)
  (import ./lastversion.nix)
  (import ./man-pages-fix.nix)
  (import ./maven.nix)
  (import ./nightlight.nix)
  (import ./percol.nix)
  (import ./prusa-slicer.nix)
  # (import ./python-hatch-vcs-fix.nix)
  # (import ./tmux.nix)
  # Give us rust-docs.
  (import ./rust.nix)
  (import ./firefox-bin.nix)
  (import ./ghostty-bin.nix)
  (import ./signal-desktop.nix { inherit flake-inputs system; })
  (import ./claude-code.nix { inherit flake-inputs system; })
  (import ./makemkv.nix)
  # (import (builtins.fetchTarball
  #   "https://github.com/oxalica/rust-overlay/archive/master.tar.gz"))
  # (import ./openconnect-sso.nix)
  # Kept as an example of using someone else's overlay remotely.
  # (import "${builtins.fetchTarball https://github.com/vlaci/openconnect-sso/archive/master.tar.gz}/overlay.nix")
  (import ./wireguard.nix)
  (import ./yt-dlp.nix)
  (import ./zig.nix)
  (import ./zoom-us.nix)
  # Needed until https://github.com/NixOS/nixpkgs/pull/391654 is merged.
  (final: prev: {
    nc4nix = prev.nc4nix.overrideAttrs (old: {
      meta.platforms = final.lib.platforms.unix;
    });
  })
  # nixpkgs's obs-studio derivation is Linux-only.  Replace it with the
  # vendored copy of NixOS/nixpkgs#498663, which refactors the package
  # into shared/linux/darwin pieces and adds an aarch64-darwin/x86_64-darwin
  # build path.  Drop this overlay once that PR merges and the pinned
  # nixpkgs catches up.
  (final: prev: {
    obs-studio = final.qt6Packages.callPackage ../derivations/obs-studio { };
  })
  # Hash mismatch: upstream re-published the zip with different contents.
  (final: prev: {
    istat-menus = prev.istat-menus.overrideAttrs (old: {
      src = prev.fetchurl {
        url = old.src.url;
        hash = "sha256-oJApYp7ejtcMrm7CyeohV/euXYkJJ0yCRBW2i5AgcEE=";
      };
    });
  })
  # aiohttp ships a wall-clock performance test
  # (test_cookie_pattern_performance) that asserts an operation completes in
  # under 10ms.  Nix builds are not hermetic with respect to CPU scheduling, so
  # this fails intermittently on loaded build machines and blocks any system
  # that depends on aiohttp (notably octoprint).  Drop doCheck globally.
  (final: prev: {
    pythonPackagesExtensions = prev.pythonPackagesExtensions ++ [
      (python-final: python-prev: {
        aiohttp = python-prev.aiohttp.overrideAttrs (_: {
          doCheck = false;
        });
      })
    ];
  })
  (final: prev: {
    dness = final.callPackage ../derivations/dness.nix { };
  })
  # (final: prev: {
  #   # Earlier my tests didn't give me any info but I found out I'd mispelled
  #   # "signal" as "singal".  So I should try again and see if this works.
  #   # signal-desktop-bin = (import ./signal-desktop-bin.nix final prev);
  #   signal-desktop-bin = prev.signal-desktop-bin.overrideAttrs (_: let
  #     version = "7.72.1";
  #     url = "https://updates.signal.org/desktop/signal-desktop-mac-universal-${version}.dmg";
  #   in {
  #     src = prev.fetchurl {
  #       hash = "";
  #       inherit url version;
  #     };
  #   });
  # })
  # This section is for anything we have listed in ../derivations/ which we
  # intend to see moved to nixpkgs, and not a deliberate overriding.
  # We need to migrate some things here.  Scan above and figure out what to move
  # into this section.
  (final: prev: {
    bgutil-pot = final.callPackage ../derivations/bgutil-pot/default.nix { };
    makemkv-flasher =
      final.callPackage ../derivations/makemkv-flasher/default.nix
        { };
    chronicle-proxy = final.callPackage ../derivations/chronicle-proxy.nix { };
    metube = final.callPackage ../derivations/metube/default.nix { };
    confluence-markdown-exporter =
      final.callPackage ../derivations/confluence-markdown-exporter.nix
        { };
    deadbeef = final.callPackage ../derivations/deadbeef.nix { };
    elktail = final.callPackage ../derivations/elktail.nix { };
    mqttpassworder = final.callPackage ../derivations/mqttpassworder.nix { };
    musicgpt = final.callPackage ../derivations/musicgpt.nix { };
    mypaint = final.callPackage ../derivations/mypaint.nix { };
    # vlc = final.callPackage ../derivations/vlc.nix {};
    zalgo-cli = final.callPackage ../derivations/zalgo-cli.nix { };
    zsh-git-prompt = final.callPackage ../derivations/zsh-git-prompt.nix { };
  })
]
