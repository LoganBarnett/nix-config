################################################################################
# Configuration common to all hosts that are servers.
################################################################################
{
  flake-inputs,
  facts,
  host-id,
  lib,
  pkgs,
  system,
  ...
}:
{
  # Configure SSH known hosts for gitea server (needed for git+ssh flake inputs).
  programs.ssh.knownHosts."gitea.${facts.network.domain}" = {
    hostNames = [ "[gitea.${facts.network.domain}]:2222" ];
    publicKey = "ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAACAQDMvereFiYoq2bHjtLiEkTL+peEXZXAUIhZES1kf2xsxEav43NCJ+uiRePzPom2YpfdxNss9f61SL505zQNwVxwBAgl4u+mFnMa0OxLZQaJjOxO3Q8KeEJBWD2HZZZWXwevk73M1Ww/zezK+sUnUrvjHp5yVS0vogsWN/rLgQybz0WhcTkMVcC+tNbiZyeGiyGpvwNzvlxXt/JqFD5L26erpJiJuGmDwyb83l87AuzlzksRYeoRQzH0fK8i61Dk0d3r2doBM/M5fWQja+Ve/mFYgB2YgPFZZ+pcWWimwe6BaMP4+0lBiIeg5hFRgzRpuJV8f9b3HFUPyxGonbAQ2PNB4BZeVIY/vyvrMjzJnQUuVrYMqMPE3mwU+Yu2ILl/D3fhDm2RZAsoSfK22jVlz8uxggcDtVTXAXDgqx4+NPKkO2XINNw/YsGFCiqhQ2kISpde6Ep4HdHsoAxbbrZRXzYC9N63mNAEMDpVIt20c5Gq7eRcWuBI42AHZo1kcyHI7JZidhb7WQctREVhtDGdd4ypT2CROcFZZcaxYlEl0xAbXaHr1hk8DNvkxZgPQ28b7GxmM/Yl/8xZ05loI9UlXpe++ND4sgQxfe3tSwrvz/haKN3qoDrzbApiGDvoB5OJFMKY7PDTariWTOkBGudJ2VulKjZ6SO3pFfVJ/vpejF0UnQ==";
  };
  imports = [
    ../nixos-configs/audio.nix
    ../nixos-configs/chrony.nix
    ../nixos-configs/journalctl-iso8601.nix
    {
      networking.domain = facts.network.domain;
      networking.hostName = host-id;
      # Override DHCP - we know who we are.  Not actually used, but if we start
      # using NetworkManager, we'll want this.
      # By default, NixOS uses its internal "legacy" networking layer - a sort
      # of systemd-networkd light.
      # networking.networkmanager.settings.main.hostname-mode = "none";
      # # This is set by default I think, but let's be explicit so we know.
      # networking.dhcpcd.enable = true;
      # networking.dhcpcd.extraConfig = ''
      #   hostname bromine        # Explicitly send this name.
      #   # There seems to be problems with hosts built with the rpi-installer.
      #   # This fixes the sticky hostname issue.
      #   nohook hostname         # Do not accept hostname from server.
      # '';
      nixpkgs.hostPlatform = system;
      nixpkgs.overlays = (
        import ../overlays/default.nix {
          inherit flake-inputs system;
        }
      );
      system.stateVersion = "23.11";
      documentation.enable = lib.mkForce false;
    }
    # {
    #   age.rekey = {
    #     # TODO: This is the host key, and we should call it that instead of the
    #     # pub key.  The .pub is the pub key, but we also have a private key and
    #     # having that called the pub-key doesn't make sense.  Make sure to
    #     # capture other references, and rename what's already on disk.
    #     hostPubkey = ../secrets/${host-id}-pub-key.pub;
    #   };
    # }
    flake-inputs.dns-smart-block.nixosModules.default
    flake-inputs.garage-queue.nixosModules.server
    flake-inputs.garage-queue.nixosModules.worker
    flake-inputs.home-manager.nixosModules.home-manager
    flake-inputs.hyuqueue.nixosModules.default
    flake-inputs.loku.nixosModules.default
    flake-inputs.nix-hapi.nixosModules.default
    flake-inputs.openhab-flake.nixosModules.default
    flake-inputs.org-wiki.nixosModules.web
    flake-inputs.proc-siding.nixosModules.default
    flake-inputs.sonify-health.nixosModules.default
    ../nixos-modules/aeotec-z-stick-7.nix
    ../nixos-modules/amd-gpu-card.nix
    ../nixos-modules/dns-aliases.nix
    ../nixos-modules/monitors.nix
    ../nixos-modules/environment-file-secrets.nix
    ../nixos-modules/https.nix
    ../nixos-modules/ivatar.nix
    ../nixos-modules/kodi-standalone.nix
    ../nixos-modules/ldap-auth.nix
    ../nixos-modules/ldap-server.nix
    ../nixos-modules/lib-custom.nix
    ../nixos-modules/nested-submodule-config-proof.nix
    ../nixos-configs/networking-static.nix
    ../nixos-modules/nfs-consumer-facts.nix
    ../nixos-modules/nfs-mount-consumer.nix
    # This can safely be included even if the host doesn't expose NFS volumes.
    ../nixos-configs/nfs-mount-provider-from-facts.nix
    ../nixos-configs/nix-store-tools.nix
    ../nixos-modules/oidc-secrets.nix
    # TODO: Test this - I think I put this in to solve remote build issues, but
    # I don't know if it actually did anything.
    # {
    #   nix = {
    #     settings = {
    #       extra-trusted-users = [ "logan" ];
    #     };
    #   };
    # }
    ../nixos-configs/secrets.nix
    # Allow servers to consume builds from other hosts.
    ../agnostic-configs/nix-builder-consume.nix
    # TODO: Remove this and only include it on hosts that need it.  Also make it
    # use the domain.
    ../nixos-configs/tls-leaf-proton.nix
    # A server should never sleep/suspend unless we have a really good reason.
    ../nixos-configs/narcolepsy.nix
    ../nixos-configs/prometheus-client.nix
    ../nixos-modules/goss.nix
    ../nixos-modules/goss-exporter.nix
    ../nixos-modules/goss-checks.nix
    flake-inputs.lix-module.nixosModules.lixFromNixpkgs
    ../nixos-modules/meraki-provisioning.nix
    ../nixos-modules/nix-flake-environment.nix
    ../nixos-modules/optical-drive.nix
    ../nixos-modules/terminal-kiosk.nix
    ../nixos-configs/nix-store-optimize.nix
    ../nixos-configs/restic-shim.nix
    # Haven't gotten this working yet.
    # ./server-host-pub-key.nix
    ../nixos-configs/sshd.nix
    ../nixos-configs/tls-trust.nix
    ../nixos-modules/unfree-predicates.nix
    ../nixos-configs/user-can-admin.nix
    ../nixos-modules/user-lockout-schedule.nix
    ../users/logan-server.nix
    ../nixos-modules/zwave-js-ui.nix
  ];
  disabledModules = [
    "services/home-automation/zwave-js-ui.nix"
  ];

  # Every controlled Linux host exports node and systemd metrics.
  networking.monitors = [
    "node"
    "systemd"
  ];

  # This is just blindly copied from somewhere, but I don't know where.  I
  # should audit them in my Vast Quantities of Space Time™.
  boot.initrd.availableKernelModules = [
    "xhci_pci"
    "nvme"
    # There's no thunderbolt ports, so see about removing this.
    "thunderbolt"
    "usb_storage"
    "usbhid"
    "sd_mod"
  ];
  # Are all of these needed?  Perhaps just to _read_ these filesystems, such
  # as from a USB drive?  I certainly don't use all of these in partitions.
  boot.supportedFilesystems = [
    "btrfs"
    "ext2"
    "ext3"
    "ext4"
    "exfat"
    "f2fs"
    "fat8"
    "fat16"
    "fat32"
    "ntfs"
    "xfs"
  ];
  # Install terminfo entries for all packaged terminal emulators so that
  # programs like watch, htop, etc. work correctly over SSH regardless of
  # which terminal the connecting user runs.
  environment.enableAllTerminfo = true;

  environment.systemPackages = [
    # This gives us strings among other things.
    pkgs.binutils
    (pkgs.callPackage ../packages/ethernet-restart.nix { })
    # Show us details about a file.
    pkgs.file
    # Diagnostic tool for troubleshooting Nix remote build configurations.
    flake-inputs.nix-remote-builder-doctor.packages.${system}.default
    # tramp-rpc server, provisioned declaratively so a remote Emacs client can
    # reach this host via /rpc: without pushing a binary over Tramp (client uses
    # tramp-rpc-deploy-never-deploy + /run/current-system/sw/bin/tramp-rpc-server).
    flake-inputs.emacs-config.packages.${system}.tramp-rpc-server
    # Gives us ldapsearch et. al. for debugging LDAP issues.
    pkgs.openldap
    # Allow us to debug TLS issues.
    pkgs.openssl
    # Should address potential speed concerns with scp/rsync transfers.
    # I'm not sure why this wouldn't be the default, and haven't found anything
    # to that effect yet.
    # I believe this is leading to this error I see when transferring 500GB
    # files:
    # ssh_dispatch_run_fatal: Connection to 192.168.254.38 port 22: message authentication code incorrect
    # This is while using a lighter weight MAC (-o MACs=umac-64-etm@openssh.com)
    # per: https://dentarg.blog/post/186913288147/umac-64-etm
    # pkgs.openssh_hpn
    pkgs.openssh
    # Gives us lsusb which allows us to query USB devices.
    pkgs.usbutils
  ];
  # This seems to cause build issues with users-groups.json for reasons that are
  # unclear.  Disable.
  # Wipes passwords, so don't use.
  users.mutableUsers = true;

  services.goss.prometheusContentTypeFixProxy.enable = true;

  # Needed to build large dependencies, which can come from surprising places.
  # Without this, oom-killer will still kill g++ on 32GB (29GB free) hosts.
  swapDevices = [
    {
      device = "/swapfile";
      size = 16 * 1024; # 16GB.
    }
  ];

  # Ensure agenix secrets are decrypted before NixOS sets up user accounts,
  # so that hashedPasswordFile references are available during activation.
  system.activationScripts.users.deps = [ "agenixInstall" ];

  # Further make life easier for builds by lowering the OOM score of the service
  # used to build.
  systemd.services.nix-daemon.serviceConfig = {
    OOMPolicy = "continue";
    OOMScoreAdjust = -1000;
  };
}
