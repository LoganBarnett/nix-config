################################################################################
# This defines the entirety of the configuration for the silicon host.
################################################################################
{
  config,
  facts,
  lib,
  flake-inputs,
  host-id,
  pkgs,
  system,
  ...
}:
let
in
{
  imports = [
    ../nixos-configs/acme.nix
    ../nixos-configs/alertmanager.nix
    ../hardware/aeotec-z-stick-7.nix
    ../nixos-configs/openhab.nix
    ../nixos-configs/zwave-js-ui.nix
    ../nixos-configs/org-wiki.nix
    ../nixos-configs/dhcp-lease-textfile.nix
    ../nixos-configs/chronicle-proxy.nix
    ../nixos-configs/gitea.nix
    ../nixos-configs/gitea-deployment-webhooks.nix
    ../nixos-configs/garage-queue-server.nix
    ../nixos-modules/nextcloud.nix
    ../nixos-configs/notes-sync.nix
    ../nixos-configs/dns-server.nix
    ../nixos-configs/dns-smart-block.nix
    ../nixos-configs/unbound.nix
    ../nixos-modules/grafana.nix
    ../nixos-modules/makemkv-ripper.nix
    ../nixos-modules/makemkv-updater.nix
    ../nixos-modules/makemkv-keydb-updater.nix
    ../nixos-configs/matrix-server.nix
    ../nixos-configs/nfs-mount-provider-from-facts.nix
    ../nixos-configs/openldap-facts.nix
    ../nixos-modules/prometheus-server.nix
    ../nixos-configs/loku.nix
    ../nixos-configs/authelia.nix
    ../nixos-configs/immich.nix
    ../nixos-configs/ivatar.nix
    ../nixos-configs/mastodon.nix
    ../nixos-configs/metube.nix
    ../nixos-configs/porkbun-dns-email.nix
    ../nixos-configs/porkbun-dns.nix
    ../nixos-configs/sftpgo.nix
    ../nixos-modules/stalwart.nix
    ../nixos-configs/stalwart.nix
    ../nixos-configs/sonify-health-silicon.nix
    ../nixos-configs/sonify-health-goss.nix
    ../nixos-configs/nix-builder-provide.nix
    ../nixos-modules/linux-host.nix
    # TODO: Right now agenix-rekey wants to build wireguard to do the
    # generation.  This fails due to a problem with macOS building wireguard-go
    # (documented in the overlay in this repository).  It is not understood why
    # it's not using the overlays.  I've removed the root nixpkgs channel and it
    # still provides this error, even with all the settings see in
    # ../nixos-modules/nix-flake-environment.nix.  I'm at a loss here.  This
    # probably warrants a ticket and I'll get to that sometime.  I have
    # confirmed that commenting this out (at least temporarily) fixes the issue.
    # However I already have pre-generated secrets so I'm not bumping into the
    # issue currently.
    ../nixos-configs/silicon-public.nix
    # Enable to make this host the network gateway (VLANs, NAT, inter-VLAN
    # firewall).  Disable to use the consumer router as the gateway.
    # ../nixos-configs/network-gateway.nix
    ../nixos-configs/wireguard/server-standard.nix
    (
      { pkgs, ... }:
      {
        # networking.hostId is needed by the filesystem stuffs.
        # An arbitrary ID needed for zfs so a pool isn't accidentally imported on
        # a wrong machine (I'm not even sure what that means).  See
        # https://search.nixos.org/options?channel=24.05&show=networking.hostId&from=0&size=50&sort=relevance&type=packages&query=networking.hostId
        # for docs.
        # Get from an existing machine using:
        # head -c 8 /etc/machine-id
        # Generate for a new machine using:
        # head -c4 /dev/urandom | od -A none -t x4 | tr -d ' '
        networking.hostId = "d88d6477";
        nixpkgs.hostPlatform = system;
      }
    )
    # Silicon has a main disk for the OS, and then two disks for data.   One for
    # the primary data and one as an incremental backup.
    {
      imports = [
        flake-inputs.disko.nixosModules.default
      ];
      # Very relevant to disko configuration, since not having these settings
      # will cause the grub installation to fail.
      # boot.loader.systemd-boot.enable = true;
      boot.loader.systemd-boot.enable = false;
      # boot.loader.efi.canTouchEfiVariables = true;
      # boot.loader.grub = {
      #   device = "nodev";
      #   efiSupport = true;
      #   # efiInstallAsRemovable = true;
      # };
      boot.loader.grub = {
        enable = true;
        # devices = [ "/dev/disk/by-id/ata-WDC_WD80EAAZ-00BXBB0_WD-RD2G0Z2H" ];
        # device = "/dev/disk/by-id/ata-WDC_WD80EAAZ-00BXBB0_WD-RD2G0Z2H";
        # device = "nodev";
        devices = [ "nodev" ];
        # Force it to respect the device declaration sibling to this.
        # forceInstall = true;
        efiSupport = false;
      };
      disko.devices = {
        disk = {
          os = {
            type = "disk";
            device = "/dev/disk/by-id/ata-WDC_WDS100T2B0A-00SM50_200616A00BEF";
            content = {
              type = "gpt";
              efiGptPartitionFirst = false;
              partitions = {
                grub = {
                  size = "1M";
                  # BIOS boot partition.
                  type = "EF02";
                  # Lowest.
                  priority = 0;
                };
                boot = {
                  size = "512M";
                  # Linux filesystem.
                  type = "8300";
                  content = {
                    type = "filesystem";
                    format = "ext4";
                    mountpoint = "/boot";
                  };
                };
                # Hybrid support, in case we want to try UEFI again.
                # ESP = {
                #   size = "1G";
                #   type = "EF00";
                #   content = {
                #     type = "filesystem";
                #     format = "vfat";
                #     mountpoint = "/boot";
                #     mountOptions = [ "umask=0077" ];
                #   };
                # };
                root = {
                  size = "100%";
                  content = {
                    type = "filesystem";
                    format = "btrfs";
                    mountpoint = "/";
                  };
                };
              };
            };
          };
          data = {
            type = "disk";
            device = "/dev/disk/by-id/ata-WDC_WD80EAAZ-00BXBB0_WD-RD2G0Z2H";
            content = {
              type = "gpt";
              partitions = {
                data = {
                  size = "100%";
                  content = {
                    type = "lvm_pv";
                    vg = "vgData";
                  };
                };
              };
            };
          };
          backup = {
            type = "disk";
            device = "/dev/disk/by-id/ata-WDC_WD80EAAZ-00BXBB0_WD-RD2EW11H";
            content = {
              type = "gpt";
              partitions = {
                backup = {
                  size = "100%";
                  content = {
                    type = "lvm_pv";
                    vg = "vgBackup";
                  };
                };
              };
            };
          };
        };
        lvm_vg = {
          vgData = {
            type = "lvm_vg";
            lvs = {
              lvData = {
                size = "100%FREE";
                content = {
                  type = "filesystem";
                  format = "btrfs";
                  mountpoint = "/tank/data";
                };
              };
            };
          };
          vgBackup = {
            type = "lvm_vg";
            lvs = {
              lvBackup = {
                size = "100%FREE";
                content = {
                  type = "filesystem";
                  format = "btrfs";
                  mountpoint = "/tank/backup";
                };
              };
            };
          };
        };
      };
    }
  ];

  # Add secondary IPs from facts.network.hosts.silicon.extraAddresses to the
  # physical NIC.  dhcp-server.nix contributes the primary /24 address (.9) on
  # all possible interface name variants; mirror that approach here so the
  # secondary /32 addresses land on whichever name the kernel actually assigns.
  # NixOS merges list attributes across modules, so all addresses coexist.
  networking.interfaces =
    lib.genAttrs
      [
        "enp3s0"
        "eno1"
        "end0"
        "ens0"
        "eth0"
      ]
      (_: {
        ipv4.addresses = builtins.map (ipv4: {
          address = "${facts.network.subnets.barnett-main}.${toString ipv4}";
          prefixLength = 32;
        }) (builtins.attrValues facts.network.hosts.silicon.extraAddresses);
      });

  # Configure ACLs for shared media directory so both nextcloud and kodi can
  # read/write.
  systemd.services.setup-shared-media-acls = {
    description = "Set up ACLs for shared media directory";
    wantedBy = [ "multi-user.target" ];
    after = [ "tank-data.mount" ];
    serviceConfig = {
      Type = "oneshot";
      RemainAfterExit = true;
    };
    script = ''
      ${pkgs.acl}/bin/setfacl -d -m g:media-shared:rwx /tank/data/nextcloud-shared-media
      ${pkgs.acl}/bin/setfacl -m g:media-shared:rwx /tank/data/nextcloud-shared-media
    '';
  };

  # Create and configure the shared media directory before Metube starts.
  systemd.services.setup-media-dir = {
    description = "Create and set ACLs for shared media directory";
    wantedBy = [ "multi-user.target" ];
    after = [ "tank-data.mount" ];
    requires = [ "tank-data.mount" ];
    serviceConfig = {
      Type = "oneshot";
      RemainAfterExit = true;
    };
    script = ''
      mkdir -p /tank/data/media
      ${pkgs.acl}/bin/setfacl -d -m g:media-shared:rwx /tank/data/media
      ${pkgs.acl}/bin/setfacl -m g:media-shared:rwx /tank/data/media
    '';
  };

  # Btrfs deduplication for /tank/data only. Runs weekly to reclaim space from
  # duplicate files (e.g., photos re-uploaded by phones).
  environment.systemPackages = [ pkgs.duperemove ];

  systemd.services.duperemove-tank-data = {
    description = "Deduplicate files on /tank/data using duperemove";
    after = [ "tank-data.mount" ];
    serviceConfig = {
      Type = "oneshot";
      # Run with lower priority to avoid impacting other services.
      Nice = 19;
      IOSchedulingClass = "idle";
    };
    script = ''
      ${pkgs.duperemove}/bin/duperemove \
        -r \
        -d \
        --dedupe-options=same \
        --hashfile=/tank/data/.duperemove.hash \
        -h \
        /tank/data
    '';
  };

  systemd.timers.duperemove-tank-data = {
    description = "Weekly deduplication of /tank/data";
    wantedBy = [ "timers.target" ];
    timerConfig = {
      # Run Sundays at 02:00 UTC (before the 11:00 UTC / 03:00 PST backup).
      OnCalendar = "Sun *-*-* 02:00:00";
      Persistent = true;
    };
  };

  # Allow the physical eject button to open the optical drive tray.
  hardware.opticalDrive.lockDoor = false;

  # MakeMKV DVD ripper for Silicon's internal drive.
  services.makemkv-ripper = {
    enable = true;
    outputPath = "/tank/data/kodi-media";
    autoRip = true;
    ejectWhenComplete = true;
    outputUser = "root";
    outputGroup = "media-shared";
    defaultDevice = "/dev/sr0";
  };

  # Auto-update MakeMKV beta key monthly.
  services.makemkv-updater = {
    enable = true;
    user = "root";
    group = "root";
  };

  # Auto-update MakeMKV AACS key database monthly.
  services.makemkv-keydb-updater = {
    enable = true;
    user = "root";
    group = "root";
  };

  # Terminal kiosk on tty1 — shows btop on a dedicated monitor.
  services.terminal-kiosk = {
    enable = true;
    programs = [
      {
        name = "btop";
        command = "${pkgs.btop}/bin/btop";
      }
    ];
  };

  # Silicon provides builds to other hosts, so it should not consume builds
  # from itself.  Override the nix-builder-consume module imported by
  # linux-host.nix to prevent self-referential build loops.
  networking.monitors = [ "goss" ];
  nix.distributedBuilds = lib.mkForce false;
  nix.buildMachines = lib.mkForce [ ];
}
